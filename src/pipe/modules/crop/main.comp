#version 460
#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_nonuniform_qualifier    : enable

#include "shared.glsl"

layout(local_size_x = DT_LOCAL_SIZE_X, local_size_y = DT_LOCAL_SIZE_Y, local_size_z = 1) in;

// global uniform stuff about image and roi
layout(std140, set = 0, binding = 1) uniform params_t
{
  mat3 H;
  float r0, r1, r2, r3;
  float crop_x, crop_X;
  float crop_y, crop_Y;
} params;


layout( // input f16 buffer rgb
    set = 1, binding = 0
) uniform sampler2D img_in;

layout( // output f16 buffer rgb
    set = 1, binding = 1
) uniform writeonly image2D img_out;

void
main()
{
  ivec2 ipos = ivec2(gl_GlobalInvocationID);
  if(any(greaterThanEqual(ipos, imageSize(img_out)))) return;

  // go through output pixels and determine input pixel.
  // this means we go through the operations in reverse order:
  // crop, rotate, perspective correction.
  vec2 ts_in  = textureSize(img_in, 0);
  // vec2 ts_out = imageSize(img_out);

  vec2 xy = vec2(ipos.xy);

  // 1) crop: adjust output coordinates by top left corner:
  vec2 co = vec2(params.crop_x, params.crop_y);
  xy += co * ts_in;

  // 2) rotate: center of the rotation is the center of the output image
  mat2 T = mat2(params.r0, params.r1, params.r2, params.r3);
  xy = T * (xy - ts_in*.5) + ts_in*.5;

  // 3) perspective correction
  vec3 rdh = (params.H * vec3(xy.xy, 1.0)).xyz;
  vec2 rd = rdh.xy / rdh.z;

  // rounding texture access:
  // vec4 rgba = texelFetch(img_in, ivec2(rd+0.5), 0);
  // bilinear:
  // vec4 rgba = texture(img_in, rd/textureSize(img_in, 0));
  // catmull rom is a little slower (especially on intel) but results
  // in a bit more acuity:
  rd /= ts_in;
  vec4 rgba;
  if(any(lessThan(rd, vec2(0.))) || any(greaterThanEqual(rd, vec2(1.))))
    rgba = vec4(0.);
  else
    rgba = sample_catmull_rom(img_in, rd);
  imageStore(img_out, ipos, vec4(rgba.rgb, 1));
}

